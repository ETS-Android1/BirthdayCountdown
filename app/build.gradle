/*
 * *
 *  * Created by Vladimir Belov on 30.06.2021, 12:42
 *  * Copyright (c) 2018 - 2021. All rights reserved.
 *  * Last modified 24.06.2021, 23:24
 *
 */

apply plugin: 'com.android.application'

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {

    signingConfigs {
        if (keystorePropertiesFile.exists()) {
            release {
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }
        }
    }

    defaultConfig{
        resConfigs "en", "de", "ru", "es", "uk"
        //multiDexEnabled true
    }

    compileSdkVersion 31
    buildToolsVersion '32.0.0'
    //https://stackoverflow.com/questions/21405457/autoincrement-versioncode-with-gradle-extra-properties
    def versionPropsFile = rootProject.file('version.properties')
    if (versionPropsFile.canRead()) {
        Properties versionProps = new Properties()

        versionProps.load(new FileInputStream(versionPropsFile))

        def code = versionProps['VERSION_CODE'].toInteger() + 1

        versionProps['VERSION_CODE'] = code.toString()
        versionProps.store(versionPropsFile.newWriter(), null)

        defaultConfig {
            applicationId "org.vovka.birthdaycountdown"
            //https://stackoverflow.com/questions/24441178/recommended-minimum-android-app-sdk
            minSdkVersion 21 //выше или ниже 17- увеличивают объём APK, 21 - минимальная для поддержки тем и ?android:attr
            //https://stackoverflow.com/questions/26694108/what-is-the-difference-between-compilesdkversion-and-targetsdkversion
            //minSdkVersion (lowest possible) <= targetSdkVersion == compileSdkVersion (latest SDK)
            targetSdkVersion 31
            //https://developer.android.com/reference/android/os/Build.VERSION_CODES.html
            versionCode code
            versionName "1.6.6"
            vectorDrawables.useSupportLibrary = true //https://stackoverflow.com/questions/41407811/android-vectordrawables-usesupportlibrary-true-is-stopping-app
        }
    } else {
        throw new FileNotFoundException("Could not read version.properties!")
    }
    buildTypes {
        release {
            //https://stackoverflow.com/questions/3540739/how-to-programmatically-read-the-date-when-my-android-apk-was-built
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

            //debuggable true
            minifyEnabled true
            shrinkResources true
            if (keystorePropertiesFile.exists()) {
                signingConfig signingConfigs.release
            }
            //https://habr.com/ru/post/415499/
            //https://www.guardsquare.com/en/products/proguard/manual/retrace
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "java.util.Date", "BUILD_TIME", "new java.util.Date(" + System.currentTimeMillis() + "L)"

            minifyEnabled false
            shrinkResources false
            debuggable true
            jniDebuggable true
            renderscriptDebuggable = true
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        //coreLibraryDesugaringEnabled true
    }

    //https://stackoverflow.com/questions/28249036/app-release-apk-how-to-change-this-default-generated-apk-name
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            outputFileName = new File("BirthdayCountdown_" + variant.versionName + ".apk")
        }
    }

}

dependencies {
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.0.2' //https://issuetracker.google.com/issues/140602653
    //implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta5' //https://androidstudio.googleblog.com/2019/12/constraintlayout-200-beta-5.html
    //implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    //noinspection GradleDependency
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    //implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0" //нужна для com.google.android.material:material:1.1.0 даёт +100кб
    //implementation 'androidx.preference:preference:1.1.1'
    //coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:1.0.10'
}
